function greptext(pattern,text)

if (pattern='' | text='')
  say 'function greptext(pattern, text)'
  say ''
  say 'Description: '
  say '   Function greptext can be used to grep only those lines from'
  say '   a text that match a given pattern. The result is the number'
  say '   of matching lines. The actual lines are stored in a global'
  say '   vector named _greptext'
  say ''
  say 'Example usage in a script: '
  say ''
  say '   "q ctlinfo"'
  say '   nlines=greptext("xdef",result)'
  say ''
  say '   say "Number of gridboxes along x is: "subwrd(_greptext.1,2)'
  exit
endif

crazy='/tmp/n1j2j3i4u5h6b7'
crazi='/tmp/8qw7w7e6r6r6t5'
rc=write(crazy,text)
rc=close(crazy)

'!grep -i 'pattern' 'crazy' > 'crazi
rc=read(crazi)
ok=sublin(rc,1)
lin=sublin(rc,2)
n=0
while(ok=0)
  n=n+1
  _greptext.n=lin

  rc=read(crazi)
  ok=sublin(rc,1)
  lin=sublin(rc,2)
endwhile
rc=close(crazi)

'!rm -f 'crazy' 'crazi

return n

*fim